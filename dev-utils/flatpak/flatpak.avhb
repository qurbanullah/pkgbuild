# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=flatpak
pkgname=(flatpak flatpak-docs)
pkgver=1.15.6
pkgrel=1
pkgdesc="Linux application sandboxing and distribution framework (formerly xdg-app)"
url="http://flatpak.org"
arch=('x86_64')
groups=('app-emultion')
license=(LGPL2.1)
depends=(
  appstream
  bubblewrap
  curl
  dbus
  fuse3
  glib2
  json-glib
  libarchive
  libmalcontent
  libseccomp
  libxau
  ostree
  polkit
  python
  systemd
  xdg-dbus-proxy
  xdg-utils
)
makedepends=(
  docbook-xsl
  git
  gobject-introspection
  gtk-doc
  meson
  python-pyparsing
  xmlto
)
checkdepends=(
  socat
  valgrind
)
source=("https://github.com/flatpak/flatpak/releases/download/$pkgver/flatpak-$pkgver.tar.xz"
        flatpak-bindir.sh)

prepare() {
  cd $pkgname-$pkgver

  # Fix tests
  patch -Np1 -i ../0002-tests-Don-t-use-g_test_message-before-g_test_init.patch

  # Support fuse3
  # https://bugs.archlinux.org/task/75623
  patch -Np1 -i ../fusermount3.diff

  # https://github.com/flatpak/flatpak/issues/267
  sed -i '/locale\/C\./d' tests/make-test-runtime.sh
}

build() {
    # export LANG='en_US'
  local meson_options=(
    -D dbus_config_dir=/usr/share/dbus-1/system.d
    -D selinux_module=disabled
    -D system_bubblewrap=bwrap
    -D system_dbus_proxy=xdg-dbus-proxy
    -D docbook_docs=disabled
    -D gtkdoc=disabled
  )

  avouch-meson flatpak-$pkgver build "${meson_options[@]}"
  meson compile -C build
}

# check() {
#   meson test -C build --print-errorlogs
# }

_pick() {
  local p="$1" f d; shift
  for f; do
    d="$srcdir/$p/${f#$pkgdir/}"
    mkdir -p "$(dirname "$d")"
    mv "$f" "$d"
    rmdir -p --ignore-fail-on-non-empty "$(dirname "$f")"
  done
}

package_flatpak() {
  depends+=(
    libostree-1.so
    xdg-desktop-portal
  )
  provides=(libflatpak.so)

  meson install -C build --destdir "$pkgdir"

  install -Dt "$pkgdir/etc/profile.d" -m644 flatpak-bindir.sh
  install -Dt "$pkgdir/etc/flatpak/remotes.d" flathub.flatpakrepo

  # Fixup mode to match polkit
  install -d -o root -g 70 -m 750 "$pkgdir/usr/share/polkit-1/rules.d"

  _pick docs "$pkgdir"/usr/share/{doc,gtk-doc}
}

package_flatpak-docs() {
  pkgdesc+=" (documentation)"
  depends=()
  mv docs/* "$pkgdir"
}
