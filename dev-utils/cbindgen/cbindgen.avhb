# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=cbindgen
pkgver=0.24.3
pkgrel=1
pkgdesc="A tool for generating C bindings to Rust code"
url="https://github.com/eqrion/cbindgen"
arch=('x86_64')
groups=('dev-utils')
license=(MPL2)
depends=(gcc-libs)
makedepends=(cargo)
source=("https://github.com/eqrion/cbindgen/archive/v$pkgver.tar.gz")

prepare() {
  cd $pkgname-$pkgver
  cargo fetch --locked --target x86_64-unknown-linux-gnu
}

build() {
  cd $pkgname-$pkgver
  cargo build --release --frozen --all-targets
}

check() {
  cd $pkgname-$pkgver
  # test_expand* fails; needs nightly rust
  RUSTC_BOOTSTRAP=1 cargo test --release --frozen
}

package() {
  cd $pkgname-$pkgver
  install -Dt "$pkgdir/usr/bin" target/release/cbindgen
  install -Dt "$pkgdir/usr/share/doc/$pkgname" -m644 README.md
}