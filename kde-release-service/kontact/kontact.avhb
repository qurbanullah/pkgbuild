# $Id$
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kontact
pkgver=22.04.0
pkgrel=1
pkgdesc='Personal Information Manager'
arch=('x86_64')
groups=('kde-apps')
url="http://kde.org/applications/office/kontact/"
license=(LGPL)
depends=('kdepim-common' 'kontactinterface')
makedepends=(extra-cmake-modules python)
source=("http://download.kde.org/stable/applications/$pkgver/src/$pkgname-$pkgver.tar.xz"{,.sig})
md5sums=('2bea6ee5151f0506d2b0d6b604594076'
         'SKIP')
validpgpkeys=(CA262C6C83DE4D2FB28A332A3A6A4DB839EAA6D7) # Albert Astals Cid <aacid@kde.org>

prepare() {
  mkdir -p build
}

build() {
  cd build
  cmake ../$pkgname-$pkgver \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DKDE_INSTALL_LIBDIR=lib \
    -DBUILD_TESTING=OFF
  make ${MAKEFLAGS}
}

package() {
  cd build
  make DESTDIR="$pkgdir" install
}
