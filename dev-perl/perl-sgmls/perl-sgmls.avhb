# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=perl-sgmls
_realname=SGMLSpm
pkgver=1.1
pkgrel=1
pkgdesc="A Post-Processor for SGMLS and NSGMLS"
arch=('any')
groups=('dev-perl')
url="https://search.cpan.org/dist/SGMLSpm"
license=('GPL' 'PerlArtistic')
depends=('perl')
options=('!emptydirs' 'docs')
source=(https://search.cpan.org/CPAN/authors/id/R/RA/RAAB/SGMLSpm-$pkgver.tar.gz)

prepare() {
	cd "$srcdir/$_realname-$pkgver"
	find . -type f -exec chmod 0644 {} \;
}

prepareEnvironment() {
	cd "$srcdir/$_realname-$pkgver"
	export \
		PERL_MM_USE_DEFAULT=1 \
		PERL_AUTOINSTALL=--skipdeps \
		PERL_MM_OPT="INSTALLDIRS=vendor DESTDIR='"$pkgdir"'" \
		PERL_MB_OPT="--installdirs vendor --destdir '"$pkgdir"'" \
		MODULEBUILDRC=/dev/null
}

build() {
	prepareEnvironment
	/usr/bin/perl Makefile.PL
	make
}

check() {
	prepareEnvironment
	make test
}

package() {
	prepareEnvironment
	make install
	find "$pkgdir" -name .packlist -o -name perllocal.pod -delete

	ln -sf sgmlspl.pl "$pkgdir"/usr/bin/sgmlspl
}