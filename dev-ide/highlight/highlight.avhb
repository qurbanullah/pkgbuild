# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=highlight
pkgname=(highlight highlight-gui)
pkgver=3.56
pkgrel=1
url="http://www.andre-simon.de/doku/highlight/highlight.html"
license=('GPL')
arch=('x86_64')
groups=('dev-ide')
makedepends=(qt5-base lua boost)
source=(http://www.andre-simon.de/zip/$pkgname-$pkgver.tar.bz2{,.asc})
validpgpkeys=(B8C55574187F49180EDC763750FE0279D805A7C7)

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"

  sed -i 's/QMAKE=qmake/QMAKE=qmake-qt5/' src/makefile
  sed -i 's/QMAKE_CC = clang/QMAKE_CC = gcc/' src/gui-qt/highlight.pro
  sed -i 's/QMAKE_CXX = clang++/QMAKE_CXX = g++/' src/gui-qt/highlight.pro
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  make ${MAKEFLAGS} QMAKE=qmake-qt5
  make ${MAKEFLAGS} QMAKE=qmake-qt5 gui
}

package_highlight() {
  pkgdesc="Fast and flexible source code highlighter (CLI version)"
  depends=('lua')
  backup=(etc/highlight/filetypes.conf)
  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" QMAKE=qmake-qt5 install
}

package_highlight-gui() {
  pkgdesc="Fast and flexible source code highlighter (Qt version)"
  depends=('qt5-base' 'highlight')
  cd "$srcdir/$pkgbase-$pkgver"

  install -dm755 "$pkgdir/usr/bin"
  make DESTDIR="$pkgdir" QMAKE=qmake-qt5 install-gui
}
