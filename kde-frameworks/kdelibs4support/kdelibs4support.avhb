# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kdelibs4support
pkgver=5.93.0
pkgrel=1
pkgdesc='Porting aid from KDELibs4'
arch=('x86_64')
groups=('kde-frameworks')
url='https://projects.kde.org/projects/frameworks/kdelibs4support'
license=('LGPL')
depends=('kunitconversion' 'kdesignerplugin' 'kinit' 'kitemmodels' 'kemoticons')
makedepends=('extra-cmake-modules' 'kdoctools' 'qt5-tools' 'networkmanager' 'perl-uri')
replaces=('kde4support')
conflicts=('kde4support')
source=("http://download.kde.org/stable/frameworks/${pkgver%.*}/portingAids/${pkgname}-${pkgver}.tar.xz")
md5sums=('31293fec18749f0c0d8aad4be2ed27df')

build() {
	cmake -S ${pkgname}-${pkgver} -B build -G Ninja \
		-DCMAKE_INSTALL_PREFIX=/usr \
		-DCMAKE_INSTALL_LIBEXECDIR=lib \
		-DLIB_INSTALL_DIR=lib \
		-DBUILD_TESTING=OFF \
		-DBUILD_QCH=ON
	ninja -C build
}

package() {
	DESTDIR="${pkgdir}" ninja -C build install
	# cert bundle seems to be hardcoded
	# link it to the one from ca-certificates
	rm -f "$pkgdir"/usr/share/kf5/kssl/ca-bundle.crt
	ln -sf /etc/ssl/certs/ca-certificates.crt "${pkgdir}"/usr/share/kf5/kssl/ca-bundle.crt
}
