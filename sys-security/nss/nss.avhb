# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=nss
pkgname=(nss ca-certificates-mozilla)
pkgver=3.79
pkgrel=1
pkgdesc="Network Security Services"
url="https://developer.mozilla.org/en-US/docs/Mozilla/Projects/NSS"
arch=('x86_64')
groups=('sys-security')
license=(MPL GPL)
_nsprver=4.21
depends=("nspr>=${_nsprver}" sqlite zlib sh p11-kit)
makedepends=(perl python3 xmlto docbook-xsl gyp)
options=(!strip !makeflags staticlibs)
source=("https://ftp.mozilla.org/pub/security/nss/releases/NSS_${pkgver//./_}_RTM/src/nss-${pkgver}.tar.gz"
        certdata2pem.py bundle.sh nss.pc.in nss-config.in nss-config.xml enable-libpkix.patch)

prepare() {
	mkdir certs

	cd nss-${pkgver}

	ln -srf nss/lib/ckfw/builtins/certdata.txt ../certs/
	ln -srf nss/lib/ckfw/builtins/nssckbi.h ../certs/
}

build() {
  cd certs
  ../certdata2pem.py

  cd ..
  sh bundle.sh

  # Work around false-positive warnings with gcc-11 (from fedora)
  export CFLAGS="$CFLAGS -Wno-array-parameter"

  # enable libpkix, without it browsers other than firefox not working
  cd nss-$pkgver/nss
    ./build.sh -v \
      --target x64 \
      --opt \
      --system-sqlite \
      --system-nspr \
      --enable-libpkix \
      --disable-tests
}

package_nss() {
  cd nss-$pkgver

  { read _vmajor; read _vminor; read _vpatch; } \
    < <(awk '/#define.*NSS_V(MAJOR|MINOR|PATCH)/ {print $3}' nss/lib/nss/nss.h)

  sed ../nss.pc.in \
    -e "s,%libdir%,/usr/lib,g" \
    -e "s,%prefix%,/usr,g" \
    -e "s,%exec_prefix%,/usr/bin,g" \
    -e "s,%includedir%,/usr/include/nss,g" \
    -e "s,%NSPR_VERSION%,${_nsprver},g" \
    -e "s,%NSS_VERSION%,${pkgver},g" |
    install -Dm644 /dev/stdin "$pkgdir/usr/lib/pkgconfig/nss.pc"
    
  ln -s nss.pc "$pkgdir/usr/lib/pkgconfig/mozilla-nss.pc"

  sed ../nss-config.in \
    -e "s,@libdir@,/usr/lib,g" \
    -e "s,@prefix@,/usr/bin,g" \
    -e "s,@exec_prefix@,/usr/bin,g" \
    -e "s,@includedir@,/usr/include/nss,g" \
    -e "s,@MOD_MAJOR_VERSION@,${_vmajor},g" \
    -e "s,@MOD_MINOR_VERSION@,${_vminor},g" \
    -e "s,@MOD_PATCH_VERSION@,${_vpatch},g" |
    install -D /dev/stdin "$pkgdir/usr/bin/nss-config"

  cd nss/doc/nroff
  install -Dt "$pkgdir/usr/share/man/man1" -m644 *util.1 signtool.1 signver.1 ssltap.1

  cd ../../../dist
  install -Dt "$pkgdir/usr/include/nss" -m644 public/nss/*.h

  cd Release/bin
  install -Dt "$pkgdir/usr/bin" *util shlibsign signtool signver ssltap

  cd ../lib
  install -Dt "$pkgdir/usr/lib" *.so
  install -Dt "$pkgdir/usr/lib" -m644 *.chk

  # Replace built-in trust with p11-kit connection
  ln -sf pkcs11/p11-kit-trust.so "$pkgdir/usr/lib/libnssckbi.so"
}

package_ca-certificates-mozilla() {
  pkgdesc="Mozilla's set of trusted CA certificates"
  depends=(ca-certificates-utils)

	cd $srcdir
	install -Dm644 ca-bundle.trust.p11-kit \
		"$pkgdir/usr/share/ca-certificates/trust-source/mozilla.trust.p11-kit"
}
