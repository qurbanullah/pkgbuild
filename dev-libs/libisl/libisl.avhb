# Copyright 2010-2022 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=libisl
_realname=${pkgname#lib}
pkgver=0.25
pkgrel=1
pkgdesc="Library for manipulating sets and relations of integer points bounded by linear constraints"
arch=('x86_64')
groups=('dev-libs')
url='https://libisl.sourceforge.io'
depends=('gmp')
provides=('isl' 'libisl.so')
license=('MIT')
source=("https://libisl.sourceforge.io/isl-${pkgver}.tar.xz")

build() {
  cd "${srcdir}"/${_realname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd "${srcdir}"/${_realname}-${pkgver}
  make check || true
}

package() {
  cd "${srcdir}"/${_realname}-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/usr/share/gdb/auto-load/usr/lib/
  mv "${pkgdir}"/usr/lib/libisl.so.*-gdb.py "${pkgdir}"/usr/share/gdb/auto-load/usr/lib/
  
  install -Dm644 LICENSE "${pkgdir}"/usr/share/licenses/${_realname}/LICENSE
}