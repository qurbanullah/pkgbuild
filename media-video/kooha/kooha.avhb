# Copyright 2010-2023 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=kooha
_realname=Kooha
pkgver=2.2.3
pkgrel=1
pkgdesc="Elegantly record your screen"
arch=('x86_64')
groups=('media-video')
url="https://github.com/SeaDve/Kooha"
license=('GPL3')
depends=('gst-plugin-pipewire' 'gstreamer-vaapi' 'gst-plugins-ugly' 'gtk4' 'libadwaita'
         'x264' 'xdg-desktop-portal' 'libpulse')
makedepends=('cargo' 'meson')
checkdepends=('appstream-glib' 'xorg-server-xvfb')
sha256sums=('3fb2fea4e8aaceb24d6bae12bdd13bdc2f7d2d5665536fe8e57f7ca20e7c2591')
source=("$pkgname-$pkgver.tar.gz::$url/archive/v$pkgver.tar.gz")

prepare() {
  cd Kooha-$pkgver
  export RUSTUP_TOOLCHAIN=stable
  cargo fetch --target "$CARCH-unknown-linux-gnu"
}

build() {
  export RUSTUP_TOOLCHAIN=stable
  avouch-meson Kooha-$pkgver build
  meson compile -C build
}

# https://github.com/SeaDve/Kooha/issues/197
#check() {
#  xvfb-run meson test -C build --print-errorlogs
#}

package() {
  meson install -C build --destdir "$pkgdir"
}