# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=docbook2x
_realname=docbook2X
pkgver=0.8.8
pkgrel=1
pkgdesc="A software package that converts DocBook documents into the traditional Unix man page format and the GNU Texinfo format."
arch=('x86_64')
groups=('app-text')
url="http://docbook2x.sourceforge.net"
license=("MIT")
depends=('glibc' 'perl-sgmls' 'perlxml' 'perl-xml-sax' 'libxslt' 'jade')
makedepends=('texinfo')
install=docbook2x.install
source=(http://downloads.sourceforge.net/$pkgname/docbook2X-$pkgver.tar.gz
        datadir.diff)

prepare() {
	cd ${srcdir}/docbook2X-${pkgver}

	patch -p1 <$srcdir/datadir.diff

	sed -e 's/AM_CONFIG_HEADER(/AC_CONFIG_HEADERS(/' -i configure.ac

	autoreconf -i -f
}

build()  {
	cd ${srcdir}/docbook2X-${pkgver}
	# to avoid file conflicts with docbook2* from docbook-utils
	./configure \
		--prefix=/usr \
		--mandir=/usr/share/man \
		--program-transform-name 's,docbook2,db2x_docbook2,'
	make
}

package() {
	cd ${srcdir}/docbook2X-${pkgver}
	make DESTDIR=$pkgdir install
	install -D -m0644 COPYING $pkgdir/usr/share/licenses/$pkgname/license
	sed -i 's|http://docbook2x.sf.net/latest|/usr/share/docbook2X|g' \
	$pkgdir/usr/bin/db2x_xsltproc
	# license
	install -D -m644 COPYING ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}
