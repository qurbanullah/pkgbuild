# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgname=tidy
pkgver=5.7.28
pkgrel=1
pkgdesc="A tool to tidy down your HTML code to a clean style"
arch=(x86_64)
groups=('app-text')
url="http://www.html-tidy.org/"
license=(custom)
depends=(glibc)
makedepends=(cmake libxslt)
source=("https://github.com/htacg/tidy-html5/archive/$pkgver.tar.gz")

prepare() {
    mkdir -p build
}

build() {
    cd build
    cmake ../tidy-html5-$pkgver \
        -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=Release
    make ${MAKEFLAGS}
}

package() {
    cd build
    make DESTDIR="$pkgdir" install

    # Compatibility symlinks until everything is ported
    ln -s tidybuffio.h "$pkgdir"/usr/include/buffio.h
    ln -s tidyplatform.h "$pkgdir"/usr/include/platform.h

    install -Dm644 "$srcdir"/$pkgname-html5-$pkgver/README/LICENSE.md "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
