# Copyright 2010-2020 Avouch Authors
# Distributed under the terms of the GNU General Public License v2
# Maintainer: Qurban Ullah <qurbanullah@avouch.org>
# Contributor: Qurban Ullah <qurbanullah@avouch.org>

pkgbase=docutils
pkgname=('python3-docutils')
pkgver=0.16
pkgrel=1
pkgdesc='Set of tools for processing plaintext docs into formats such as HTML, XML, or LaTeX'
arch=('any')
groups=('app-text')
url='http://docutils.sourceforge.net'
license=('custom')
makedepends=('python3')
source=("http://downloads.sourceforge.net/$pkgbase/$pkgbase-$pkgver.tar.gz")

build() {
  cd $pkgbase-$pkgver
  python3 setup.py build
}

check() {
  cd $pkgbase-$pkgver
  # we need utf locale to valid utf8 tests
  export LANG=en_US.UTF-8
#   PYTHONPATH="$PWD/build/python3/" python3 test/alltests.py
}

package() {
  cd $pkgbase-$pkgver
  python3 setup.py build install --root="$pkgdir" --optimize=1
  # symlink without .py
  for f in "$pkgdir"/usr/bin/*.py; do
      ln -s "$(basename $f)" "$pkgdir/usr/bin/$(basename $f .py)"
  done
  # setup license
  install -D -m644 COPYING.txt "$pkgdir/usr/share/licenses/$pkgname/COPYING.txt"
  install -D -m644 licenses/python* "$pkgdir/usr/share/licenses/$pkgname/"
}